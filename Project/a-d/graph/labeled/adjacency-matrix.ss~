#lang r6rs

;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*                                                                 *-*-
;-*-*         Labeled Graphs (Adjacency Matrix Representation)        *-*-
;-*-*                                                                 *-*-
;-*-*                       Wolfgang De Meuter                        *-*-
;-*-*                   2009  Software Languages Lab                  *-*-
;-*-*                    Vrije Universiteit Brussel                   *-*-
;-*-*                                                                 *-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-

(library
 (labeled-graph)
 (export new labeled-graph? order directed? nr-of-edges
         for-each-node for-each-edge
         add-edge! delete-edge!
         adjacent?
         label label! edge-label)
 (import (rnrs base)
         (rnrs control)
         (rnrs mutable-pairs))
 
 (define labeled-graph 'labeled-graph)
 
 (define (make directed nr-of-nodes)
   (list labeled-graph
         directed
         0 ; nr-of-edges
         (let ((rows (make-vector nr-of-nodes)))
           (let fill-row
             ((i 1))
             (vector-set! rows (- i 1) (cons 'no-label (make-vector nr-of-nodes 'no-label)))
             (if (< i nr-of-nodes)
                 (fill-row (+ i 1))
                 rows)))))
 
 (define (storage graph)
   (cadddr graph))
 
 (define (directed? graph)
   (cadr graph))
 
 (define (nr-of-edges graph)
   (caddr graph))
 
 (define (nr-of-edges! graph nr)
   (set-car! (cddr graph) nr))
 
 (define (new directed nr-of-nodes)
   (make directed nr-of-nodes))
 
 (define (labeled-graph? sobj)
   (and (pair? sobj)
        (eq? (car sobj) labeled-graph)))
 
 (define (order graph)
   (vector-length (storage graph)))
 
 (define (for-each-node graph proc)
   (define rows (storage graph))
   (let iter-nodes
     ((i 0))
     (proc i (car (vector-ref rows i)))
     (if (< (+ i 1) (order graph))
         (iter-nodes (+ i 1))))
   graph)
 
 (define (for-each-edge graph node proc)
   (define rows (storage graph))
   (let iter-edges
     ((to 0)
      (label (vector-ref (cdr (vector-ref rows node)) 0)))
     (if (not (eq? label 'no-label))
         (proc to label))
     (if (< (+ to 1) (order graph))
         (iter-edges (+ to 1) (vector-ref (cdr (vector-ref rows node)) (+ to 1)))))
   graph)
 
 (define (label! graph node label)
   (define rows (storage graph))
   (set-car! (vector-ref rows node) label)
   graph)
 
 (define (label graph node)
   (define rows (storage graph))
   (car (vector-ref rows node)))
 
 (define (add-edge! graph from to label)
   (define rows (storage graph))
   (vector-set! (cdr (vector-ref rows from)) to label)
   (when (not (directed? graph))
     (vector-set! (cdr (vector-ref rows to)) from label)
     (nr-of-edges! graph (+ 1 (nr-of-edges graph))))
   graph)
 
 (define (delete-edge! graph from to)
   (define rows (storage graph))
   (vector-set! (cdr (vector-ref rows from)) to 'no-label)
   (when (not (directed? graph))
     (vector-set! (cdr (vector-ref rows to)) from 'no-label)
     (nr-of-edges! graph (- (nr-of-edges graph) 1)))
   graph)
 
 (define (adjacent? graph from to)
   (define rows (storage graph))
   (define row (cdr (vector-ref rows from)))
   (not (eq? (vector-ref row to) 'no-label)))
 
 (define (edge-label graph from to)
   (define rows (storage graph))
   (if (eq? (vector-ref (cdr (vector-ref rows from)) to) 'no-label)
       #f
       (vector-ref (cdr (vector-ref rows from)) to))))